// Project:
//
// - Build Tool: Gradle - Groovy
// - Language: Java
// - Spring Boot: 3.3.5
// - Packaging: Jar
// - Java Version: 17
//

// Project Metadata:
//
// - Group: kr.co.road2gm
// - Artifact: api
// - Name: api
// - Description: Road2GM backend API server
// - Package name: kr.co.road2gm.api
//

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'kr.co.road2gm'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

//Dependencies:
//
// - Spring Web
// - Spring Data JPA
// - Spring Security
// - Lombok
// - Validation
// - MariaDB Driver
// - Spring Boot DevTools
// - Spring Configuration Processor

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// JJWT-API - 2024-10-27 (버전 명시)
	// https://github.com/jwtk/jjwt
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6' // or 'io.jsonwebtoken:jjwt-gson:0.12.6' for gson

	// QueryDSL - 2024-10-27 (버전/태그 명시): https://mvnrepository.com/artifact/com.querydsl/querydsl-jpa
	// SpringBoot 3.x + QueryDSL 5.1.0 (Use `jakarta package, but Don't use QueryDSL plugin)
	implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
}

tasks.named('test') {
	useJUnitPlatform()
}

// 버전 태그 없이 jar 파일 빌드, 배포 때 용이
bootJar {
	archiveFileName = "${rootProject.name}.jar"
}

// no plain jar
jar {
	enabled = false
}